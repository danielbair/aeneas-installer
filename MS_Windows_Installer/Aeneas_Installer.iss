; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "aeneas tools"
#define MyAppVersion "1.7.3.0"
#define MyAppPublisher "Daniel Bair"
#define MyAppURL "http://www.danielbair.com/"
#define MyAppInstallDir "C:\aeneas-install"
#define MyAppFileName "aeneas-windows-setup-1.7.3.0_3"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A15A9B64-C0EF-4E13-926C-0C60A8FA9FBC}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
ArchitecturesInstallIn64BitMode=x64
UsePreviousAppDir=yes
DefaultDirName={#MyAppInstallDir}
DisableDirPage=yes
DisableWelcomePage=no
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
DisableProgramGroupPage=yes
LicenseFile=LICENSE.txt
InfoBeforeFile=README.txt
OutputDir=.\
OutputBaseFilename={#MyAppFileName}
Compression=lzma
SolidCompression=yes
ChangesEnvironment=yes
;SignTool=mysigntool

[Messages]
WelcomeLabel2=This will install aeneas 1.7.3.0 on your computer.%n%naeneas is a Python library and a set of tools for automated audio and text synchronization.%n%nIn addition to aeneas, the following independent programs necessary for running aeneas are contained in this installer:%n1. eSpeak-NG%n2. FFmpeg%n3. Python%n%nIt is recommended that you close all other applications before continuing.

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Components]
Name: "espeak"; Description: "Install eSpeak-NG 1.50"; ExtraDiskSpaceRequired: 11223040; Types: full custom
Name: "ffmpeg"; Description: "Install FFmpeg 4.3"; ExtraDiskSpaceRequired: 111181824; Types: full custom
Name: "python"; Description: "Install Python 3.8.5"; ExtraDiskSpaceRequired: 106450944; Types: full
Name: "bs4"; Description: "Install Python Module BeautifulSoup4 4.9.1"; ExtraDiskSpaceRequired: 3400000; Types: full compact custom
Name: "lxml"; Description: "Install Python Module lxml 4.5.2"; ExtraDiskSpaceRequired: 3548446; Types: full compact custom
Name: "numpy"; Description: "Install Python Module NumPy 1.19.1"; ExtraDiskSpaceRequired: 12971083; Types: full compact custom
Name: "aeneas"; Description: "Install Python Module aeneas 1.7.3.0"; ExtraDiskSpaceRequired: 4885092; Types: full compact custom
; NOTE: Previous used "Flags: fixed" on each component

[Files]
Source: "aeneas-1.7.3.0-cp38-cp38-win_amd64.whl"; DestDir: "{app}"; Components: aeneas; Flags: ignoreversion
Source: "numpy-1.19.1-cp38-cp38-win_amd64.whl"; DestDir: "{app}"; Components: numpy; Flags: ignoreversion
Source: "lxml-4.5.2-cp38-cp38-win_amd64.whl"; DestDir: "{app}"; Components: lxml; Flags: ignoreversion
Source: "beautifulsoup4-4.9.1-py3-none-any.whl"; DestDir: "{app}"; Components: bs4; Flags: ignoreversion
Source: "soupsieve-2.0.1-py3-none-any.whl"; DestDir: "{app}"; Components: bs4; Flags: ignoreversion
Source: "pip-20.1.1-py2.py3-none-any.whl"; DestDir: "{app}"; Components: python; Flags: ignoreversion
Source: "python-3.8.5-amd64.exe"; DestDir: "{app}"; Components: python; Flags: ignoreversion
Source: "ffmpeg-4.3-win64-static.exe"; DestDir: "{app}"; Components: ffmpeg; Flags: ignoreversion
Source: "espeak-ng-1.50-x64.msi"; DestDir: "{app}"; Components: espeak; Flags: ignoreversion
Source: "espeak-ng.lib"; DestDir: "{app}"; Components: espeak; Flags: ignoreversion
Source: "install_packages.bat"; DestDir: "{app}"; Components: aeneas; Flags: ignoreversion
Source: "aeneas_check_setup.bat"; DestDir: "{app}"; Components: aeneas; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\ffmpeg-4.3-win64-static.exe"; Parameters: "/SILENT /ALLUSERS"; Description: "Install FFmpeg 4.3"; Components: ffmpeg; Flags: shellexec waituntilterminated
Filename: "{app}\espeak-ng-1.50-x64.msi"; Parameters: "/PASSIVE InstallAllUsers=1 PrependPath=1"; Description: "Install eSpeak-NG 1.50"; Components: espeak; Flags: shellexec waituntilterminated
Filename: "{app}\python-3.8.5-amd64.exe"; Parameters: "/PASSIVE InstallAllUsers=1 PrependPath=1 TargetDir=C:\Program Files\Python38"; Description: "Install Python 3.8.5"; Components: python; Flags: shellexec waituntilterminated
Filename: "{app}\install_packages.bat"; Description: "Install Aeneas 1.7.3.0 and dependencies"; Components: aeneas; Flags: shellexec waituntilterminated
Filename: "{app}\aeneas_check_setup.bat"; Description: "Check Aeneas Setup"; Components: aeneas; Flags: shellexec waituntilterminated

[UninstallRun]
Filename: "{commonpf64}\FFmpeg\unins000.exe"; Parameters: "/SILENT"; Components: ffmpeg; Flags: shellexec waituntilterminated
Filename: "{sys}\MSIEXEC.EXE"; Parameters: "/PASSIVE /X {app}\espeak-ng-1.50-x64.msi"; Components: espeak; Flags: shellexec waituntilterminated
Filename: "{sys}\MSIEXEC.EXE"; Parameters: "/PASSIVE /X {app}\python-3.8.5-amd64.exe"; Components: python; Flags: shellexec waituntilterminated

[UninstallDelete]
Type: filesandordirs; Name: "{commonpf64}\FFmpeg"; Components: ffmpeg
Type: filesandordirs; Name: "{commonpf64}\Python38"; Components: python
Type: filesandordirs; Name: "{commonpf64}\eSpeak NG"; Components: espeak
Type: filesandordirs; Name: "{win}\System32\espeak-ng.dll"; Components: espeak
Type: filesandordirs; Name: "{app}";

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{userappdata}\python\python38\Scripts;{olddata}"; Components: aeneas; Check: NeedsAddPath('{userappdata}\python\python38\Scripts')
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{commonpf64}\Python38\Scripts;{olddata}"; Components: python; Check: NeedsAddPath('{commonpf64}\Python38\Scripts')
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{commonpf64}\Python38\;{olddata}"; Components: python; Check: NeedsAddPath('{commonpf64}\Python38\')
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{commonpf64}\FFmpeg\bin;{olddata}"; Components: ffmpeg; Check: NeedsAddPath(ExpandConstant('{commonpf64}\FFmpeg\bin'))
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{commonpf64}\eSpeak-NG;{olddata}"; Components: espeak; Check: NeedsAddPath(ExpandConstant('{commonpf64}\eSpeak-NG'))

[Code]
function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
begin
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment',
    'Path', OrigPath)
  then begin
    Result := True;
    exit;
  end;
  // look for the path with leading and trailing semicolon
  // Pos() returns 0 if not found
  Result := Pos(';' + Param + ';', ';' + OrigPath + ';') = 0;
end;
